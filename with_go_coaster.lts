
const Max  = 9
const MCar = 4

// models passenger arrival at the platform
PASSENGERS = (newPassenger -> PASSENGERS).

// limits passengers on platfrom to Max & allocates passengers to cars
CONTROLLER        
    = CONTROL[0][0],
CONTROL[count:0..Max][carSize:0..MCar]  
    = (when (count < Max) 
            newPassenger  -> CONTROL[count+1][carSize]
      |requestPassenger[n:1..MCar] -> CONTROL[count][n] 
      |when (carSize > 0 && count >= carSize) 
            getPassenger[carSize] -> CONTROL[count-carSize][0] // Set to 0 as the car has now left, so want to wait for new car before calling getPassengers again 
	  
	  // Specification was not clear on whether or not button can be pressed when there are enough people
	  // to fill the car completely, so have assumed you can't, by using `count < carSize`
 	  |when (carSize > 0 && count > 0 && count < carSize) 
			goNow -> getPassenger[count] -> CONTROL[0][0]
      ).

// the coaster car requests N passengers and departs when the
// controller responds with getPassenger
COASTERCAR(N=MCar) 
    = (arrive -> requestPassenger[N] -> getPassenger[i:1..MCar] -> 
         if (i > N) then ERROR else (depart -> COASTERCAR))
      +{{requestPassenger,getPassenger}[1..MCar]}.

// controls access to the platform
PLATFORMACCESS = (arrive -> depart -> PLATFORMACCESS).

// Allows car to leave if there are >1 but <MCar passengers available
BUTTON = (goNow -> BUTTON).


// system with two coaster cars with capacity two and three
||ROLLERCOASTER
         = (PASSENGERS 
           || car[1..2]::(CONTROLLER || PLATFORMACCESS || BUTTON) 
           || car[1]:COASTERCAR(2) 
           || car[2]:COASTERCAR(3)
		   
           )
           /{newPassenger/car[1..2].newPassenger}.
